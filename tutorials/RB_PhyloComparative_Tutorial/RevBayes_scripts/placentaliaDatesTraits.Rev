################################################################################
#
# RevBayes Tutorial:the phylogenetic comparative method
# 
# This file: Correlation analysis of dates and life-history traits in placental mammals
#
# authors: Nicolas Lartillot
#
################################################################################

######### 
# data
#########

# read the quantitative trait data 
contData <- readContinuousCharacterData("data/plac40lhtlog.nex")

# get some useful variables from the data
nTraits <- contData.nchar()[1]

# read the sequence data
# the readCharacter function returns a vector of matrices. We just take the first one
seqData <- readDiscreteCharacterData("data/plac40_4fold.nex")

# get some useful variables from the data
nTaxa <- seqData.ntaxa()
names <- seqData.names()
nSites <- seqData.nchar()[1]


######### 
# tree  
#########

# create a birth death process, leading to a tree whose tips match the taxon names given by the dataset

speciation ~ dnLogUniform(0.001,1000)
extinction ~ dnLogUniform(0.001,1000)

# rough estimate of sampling fraction: 40 placental mammals out of the ~4000 currently existing out there
sampling_fraction := 0.01

psi ~ dnBDP(lambda=speciation, mu=extinction, rho=sampling_fraction, rootAge=1.0, nTaxa=nTaxa, names=names)

# work under fixed, externally provided, tree topology
treeArray <- readTrees("data/chronoplac40.tree")
fixedTree <- treeArray[1]
psi.setValue(fixedTree)


######### 
# rate and evolution
#########

# covariance matrix of dimension nTraits 
# first entry will be log of substitution rate
# then all other entries will stand for the life-history traits

# covariance matrix is inverse Wishart of parameters kappa * Identity and df = nTraits+2 degrees of freedom
# where kappa is itself a scaling factor

kappa ~ dnLogUniform(min=0.001,max=1000)
kappa.setValue(0.1)
df <- nTraits+2
covdim <- nTraits

Sigma ~ dnInvWishart(dim=covdim, kappa=kappa, df=df)

# multivariate Brownian process along the tree
X ~ dnBrownianMultiVariate(psi,sigma=Sigma)

# copmonents of the Brownian process correspond to life-history traits
# so we clamp the process at the tips of the tree, at the values observed in extant taxa
# you need to do this trait by trait
for (i in 1:nTraits)	{

	# here we say that the ith entry of the Brownian process should map to the ith. quantitative trait
	X.clampAt(contData,i,i)
}

# get useful functions of the covariance matrix, to be monitored
for (i in 1:covdim)	{
	var[i] := Sigma.covariance(i,i)
}

k <- 1;
for (i in 1:(covdim-1))	{
	for (j in (i+1):covdim)	{
		correl[k] := Sigma.correlation(i,j)
		pCorrel[k] := Sigma.partialCorrelation(i,j)
		k <- k+1
	}
}

# get useful outputs from the brownian process
# the mean across the tree, for each trait
for (i in 1:covdim)	{
	meanX[i] := X.mean(i)
}

######### 
# relaxed clock
#########

# rate of variation of the log of the rate of substitution
nu ~ dnLogUniform(min=0.001,max=1000)
nu.setValue(0.1)

# create Brownian process along the tree
lograte ~ dnBrownian(psi,nu)

# two useful summary statistics for checking convergence
# mean and std dev of variation of the process along the tree
meanlograte := lograte.mean()

# in the lognormal clock, rates across branches are given by the exponential of the Brownian motion
# specifically, the rate along a branch is the mean of the exponential of the value of the Brownian motion at both ends
rateprocess := expBranchTree(tree=psi,process=lograte);

######### 
# substitution process
#########

# equilibrium frequencies 
bf <- v(1,1,1,1)
pi ~ dnDirichlet(bf)

# exchangeabilities
e <- v(1,1,1,1,1,1)
er ~ dnDirichlet(e)

# rate matrix
Q := fnGTR(er,pi)

# create the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, branchRates=rateprocess, nSites=nSites, type="DNA")

# condition on sequence alignment
seq.clamp(seqData)

# create model
mymodel = model(kappa)

######### 
# moves
#########

# initializing index
index <- 1

# rescaling moves on speciation and extinction rates
moves[index++] = mvScale(speciation, lambda=1, tune=true, weight=3.0)
moves[index++] = mvScale(extinction, lambda=1, tune=true, weight=3.0)

# moves on the tree: only relative dates
moves[index++] = mvSubtreeScale(psi, weight=5.0)
moves[index++] = mvNodeTimeSlideUniform(psi, weight=10.0)

# if we wanted to, we could also move the tree topology
# and integrate over the posterior induced by the sequence data
# but mixing is currently a bit challenging 
# moves[index++] = mvNNI(psi, weight=5.0)
# moves[index++] = mvFNPR(psi, weight=5.0)

# moves on kappa
moves[index++] = mvScale(kappa, lambda=2.0, tune=true, weight=3.0)

# conjugate Gibbs move on covariance matrix
moves[index++] = mvConjugateInverseWishartBrownian(sigma=Sigma, process=X, kappa=kappa, df=df, weight=1) 

# moves on the Brownian process
moves[index++] = mvMultivariatePhyloProcessSliding(process=X,lambda=1,tune=true,weight=100) 
moves[index++] = mvMultivariatePhyloProcessTranslation(process=X,lambda=0.1,tune=true,weight=1) 

# moves on nu
moves[index++] = mvScale(nu, lambda=2.0, tune=true, weight=3.0)

# moves on the relaxed clock process
moves[index++] = mvRealNodeValTreeSliding(process=lograte,lambda=1,tune=true,weight=100) 
moves[index++] = mvRealNodeValTreeTranslation(process=lograte,lambda=0.1,tune=true,weight=10) 

# moves on the substitution process parameters
moves[index++] = mvSimplexElementScale(pi, alpha=10.0, tune=true, weight=1.0)
moves[index++] = mvSimplexElementScale(er, alpha=10.0, tune=true, weight=1.0)

######### 
# monitors
#########

# screen monitor
monitors[1] = mnScreen(printgen=10, speciation, extinction, kappa, nu, correl, meanlograte, meanX)

# similar monitor, but directly into file
monitors[2] = mnFile(filename="output/placdatestraits.trace", printgen=10, separator = TAB, speciation, extinction, kappa, nu, correl, var, meanlograte, meanX)

# file monitors for the correlation and partial correlation coefficients
monitors[3] = mnFile(filename="output/placdatestraits.correl",printgen=10, separator = TAB, correl, pCorrel)

# a file monitor just for the covariance matrix
monitors[4] = mnFile(filename="output/placdatestraits.cov",printgen=10, separator = TAB, Sigma)

# a file monitor for the evolution of traits along the tree (in newick format)
monitors[5] = mnFile(filename="output/placdatestraits.traits",printgen=10, separator = TAB, X)

# monitoring the trees and dates
monitors[6] = mnFile(filename="output/placdatestraits.trees",printgen=10, separator = TAB, psi)

# a model monitor
monitors[7] = mnModel(filename="output/placdatestraits.log",printgen=10, separator = TAB)
 
mymcmc = mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=200,tuningInterval=50)

mymcmc.run(1000)


