################################################################################
#
# RevBayes Tutorial:the phylogenetic comparative method
# 
# This file: log-Brownian evolution of body mass in placental mammals
#
# authors: Nicolas Lartillot
#
################################################################################


# read the data 
contData <- readContinuousCharacterData("data/plac40lhtlog.nex")
"read continuous data"

# work under fixed topology
treeArray <- readTrees("data/chronoplac40.tree")
psi <- treeArray[1]
"read tree"

# sigma: variance per unit of time of the Brownian motion
sigma ~ dnLogUniform(min=0.001,max=1000)
sigma.setValue(0.1)

# univariate Brownian process along the tree
# parameterized by sigma
logmass ~ dnBrownian(psi,sigma)

"brownian motion initialized"

# moves on sigma 
index <- 1
moves[index++] = mvScale(sigma, lambda=2.0, tune=true, weight=3.0)

# moves on the Brownian process
moves[index++] = mvRealNodeValTreeSliding(process=logmass,lambda=10,tune=true,weight=100) 
moves[index++] = mvRealNodeValTreeTranslation(process=logmass,lambda=1,tune=true,weight=1) 

# condition Brownian model on quantitative trait data (second column of the dataset)
logmass.clampAt(contData,2)

"clamped"

# get useful functions of the covariance matrix, to be monitored:
str(logmass)

# the mean and the standard deviation of log body mass across the tree
meanlogmass := logmass.mean()
"mean"
stdevlogmass := logmass.stdev()
"stdev"

# the value of log body mass at the root
rootlogmass := logmass.rootVal()

"model"
# create the model
mymodel = model(sigma)

# create the monitors
# some of them are a bit redundant, but just to show what can be done

"monitors"
# on screen, we will monitor only the correlation coefficient and the mean value of each trait
monitors[1] = mnScreen(printgen=10, sigma, rootlogmass, meanlogmass, stdevlogmass)

# a trace monitor: like the screen monitor, but directly into file
monitors[2] = mnFile(filename="output/placmass.trace", printgen=10, separator = TAB, sigma, rootlogmass, meanlogmass, stdevlogmass)

# a file monitor for the evolution of body mass along the tree (in newick format)
monitors[3] = mnFile(filename="output/placmass.traits",printgen=10, separator = TAB, logmass)

# a model monitor
monitors[4] = mnModel(filename="output/placmass.log",printgen=10, separator = TAB)
 
mymcmc = mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=200,tuningInterval=50)

mymcmc.run(1000)

# some post analysis here

